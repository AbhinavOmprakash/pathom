= Resolvers

In `Connect`, you implement the graph by creating `resolvers`. Those resolvers are functions that expose some data on the graph.

A resolver has a few basic elements:

. Inputs â€“ A set of attributes that are required to be in the current parsing context
for the resolver to be able to work. Inputs is optional, no inputs means that the
resolver is always capable of working, independently of the current parsing context.
. Outputs - A query-like notation representing the shape of data the resolver is able
to resolve. This is typically a list of attributes/joins, where joins
typically include a simple subquery.
. A function - A `(fn [env input-data] tree-of-promised-output)` that takes the inputs
and turns them into a tree that satisfies the "output query".

So you might define a resolver like this:

[source,clojure]
----
(pc/defresolver person-resolver
  [{:keys [database] :as env} {:keys [person/id]}]
  {::pc/input #{:person/id}
   ::pc/output [:person/first-name :person/age]}
  (let [person (my-database/get-person database id)]
    {:person/age        (:age person)
     :person/first-name (:first-name person)}))
----

TIP: If you use link:https://cursive-ide.com/[Cursive], you can ask it to resolve the `pc/defresolver` as a `defn` and you will get proper symbol resolution

Where the `database` in the environment would be supplied when running the parser, and the input would have to be
found in the current context.  Remember that graph queries are contextual... you have to have a starting node to work
from, so in the above example we're assuming that during our parse we'll reach a point where the context contains
a `:person/id`.  The `my-database` stuff is just made up for this example, and is intended to show you that your
data source does not need to remotely match the schema of your graph query.

Pathom will scan through the defined resolvers in order to try to satisfy all of the properties in a query.  So, technically
you can split up your queries as much as makes sense into separate resolvers, and as long as the inputs are in the context
Pathom will assemble things back together.

Of course, it doesn't make sense in this case to do so, because each resolver would end up running a new query:

[source,clojure]
----
(pc/defresolver person-age-resolver [{:keys [database] :as env} {:keys [person/id]}]
  {::pc/input #{:person/id}
   ::pc/output [:person/age]}
  (let [person (my-database/get-person database id)]
    {:person/age (:age person)}))

(pc/defresolver person-first-name-resolver [{:keys [database] :as env} {:keys [person/id]}]
  {::pc/input #{:person/id}
   ::pc/output [:person/first-name]}
  (let [person (my-database/get-person database id)]
    {:person/first-name (:first-name person)}))

...
----

The point is that a single-level query like `[:person/id :person/first-name :person/age]` can be satisfied and "folded together"
by Pathom over any number of resolvers.

This fact is the basis of parser (de)composition and extensibility.  It can also come in handy for performance
refinements when there are computed attributes.

== Derived/Computed Attributes

There are times when you'd like to provide an attribute that is computed in some fashion.  You can, of course, simply
compute it within the resolver along with other properties like so:

[source,clojure]
----
(pc/defresolver person-resolver [{:keys [database] :as env} {:keys [person/id]}]
  {::pc/input #{:person/id}
   ::pc/output [:person/first-name :person/last-name :person/full-name :person/age]}
  (let [{:keys [age first-name last-name]} (my-database/get-person database id)]
    {:person/age        age
     :person/first-name first-name
     :person/last-name  last-name
     :person/full-name  (str first-name " " last-name) ; COMPUTED
     ...}))
----

but this means that you'll take the overhead of the computation when *any* query relate to person comes up.  You can
instead spread such attributes out into other resolvers as we discussed previously, which will only be invoked if the
query actually asks for those properties:

[source,clojure]
----
(pc/defresolver person-resolver [{:keys [database] :as env} {:keys [person/id]}]
  {::pc/input #{:person/id}
   ::pc/output [:person/first-name :person/last-name :person/age]}
  (let [{:keys [age first-name last-name]} (my-database/get-person database id)]
    {:person/age        age
     :person/first-name first-name
     :person/last-name  last-name}))

(pc/defresolver person-name-resolver [_ {:person/keys [first-name last-name]}]
  {::pc/input #{:person/first-name :person/last-name}
   ::pc/output [:person/full-name]}
  {:person/full-name (str first-name " " last-name)})
----

This combination of resolvers can still resolve all of the properties in `[:person/full-name :person/age]` (if
`:person/id` is in the context), but a query for just `[:person/age]` won't invoke any of the logic for the
`person-name-resolver`.

== Single Inputs -- Establishing Context [[SingleInputs]]

So far we have seen how to define a resolver that can work *as long as* the inputs are already in the environment.  You're
almost certainly wondering how to do that.

One way is to define <<GlobalResolvers,global resolvers>> and start the query from them, but very often you'd just
like to be able to say "I'd like the first name of person with id 42."

EQL uses "idents" to specify exactly that sort of query:

[source,clojure]
----
[{[:person/id 42] [:person/first-name]}]
----

The above is a join on an ident, and the expected result is a map with the ident as a key:

[source,clojure]
----
{[:person/id 42] {:person/first-name "Joe"}}
----

The query *itself* has everything you need to establish the *context* for running the `person-resolver`,
and in fact that is how Pathom single-input resolvers work.

If you use an ident in a query then Pathom is smart enough to know that it can use that ident to establish the context
for finding resolvers.  In other words, in the query above the ident `[:person/id 42]` is turned
into the *parsing context* `{:person/id 42}`, which satisfies the *input* of any resolver that needs
`:person/id` to run.

== Resolver Without Input -- Global Resolver [[GlobalResolvers]]

A resolver that requires no input can output its results at any point in the graph, thus it is really a global resolver.
Pay a particular attention to the *"at any point in the graph"* - it's not just at the root.  Thus, a resolver without inputs
can "inject" its outputs into any level of the query graph result.

We're going to start building a parser that can satisfy queries about a music store. So, we'll start with a global resolver
that can resolve the "latest product".  The code below shows the entire code needed, boilerplate and all:

[source,clojure]
----
include::example$com/wsscode/pathom/book/connect/getting_started.cljs[]
----

Our first resolver exposes the attribute `::latest-product`, and since it doesn't require any input it is a global resolver.
Also, note that our output description includes the full output details (including nested attributes), this is mostly
useful for auto-completion on UI's and automatic testing. If you return extra data it will still end up in the output
context.

Try some of these queries on the demo below:

[source,clojure]
----
[::latest-product]
[{::latest-product [:product/title]}]

; ::latest-product can be requested anywhere
[{::latest-product
  [* ::latest-product]}]
----

++++
<div x-app="interactive-parser" data-parser="connect.getting-started" class="loader">
[::latest-product]
</div>
<div class="space"></div>
++++

== Resolvers with single input

Next, let's say we want to have a new attribute which is the brand of the product. Of course, we could just throw the
data there in our other resolver, but the real power of Connect comes out when we start splitting the responsibilities
among resolvers, so let's define a resolver for brand that requires an input of `:product/id`:

[source,clojure]
----
include::example$com/wsscode/pathom/book/connect/getting_started2.cljs[]
----

++++
<div x-app="interactive-parser" data-parser="connect.getting-started2" class="loader">
[{::latest-product [:product/title :product/brand]}]
</div>
<div class="space"></div>
++++

The input is a `set` containing the keys required on the current entity in the parsing context for the resolver to be
able to work. This is where `Connect` starts to shine because any time your query asks for a bit of data
it will try to figure it out how to satisfy that request based on the attributes that the current contextual entity *already has*.

More importantly: `Connect` will *explore the dependency graph* in order to resolve things if it needs to!
To illustrate this, let's pretend we have some external ID for the brand and that we can derive this ID from the
brand string - pretty much just another mapping:

[source,clojure]
----
;; a silly pretend lookup
(def brand->id {"Taylor" 44151})

(pc/defresolver brand-id-from-name [_ {:keys [product/brand]}]
  {::pc/input #{:product/brand}
   ::pc/output [:product/brand-id]}
  {:product/brand-id (get brand->id brand)})

(comment
  (parser {} [{::latest-product [:product/title :product/brand-id]}])
  ; => #::{:latest-product #:product{:title "Acoustic Guitar", :brand-id 44151}}
)
----

Note that our query never said anything about the `:product/brand`. `Connect` automatically walked the path
`:product/id -> :product/brand -> :product/brand-id` to obtain the information desired by the query!

When a required attribute is not present in the current entity, `Connect` will look for resolvers that can fetch it,
analyze their inputs, and recursively walk backwards towards the "known data" in the context.
When a required attribute is not present in the current entity, `Connect` will calculate the possible paths
from the data you have to the data you request, then it can use some heuristic to decide which path to take
and walk this path to reach the data, if there is no possible path connect reader will return `::p/continue` to
let another reader try to handle that key. You can read more about how this works in the Index page.

Also remember that single-input resolvers can handle ident-based queries.
Thus, the following ident-join queries already work without having to define anything else:

[source,clojure]
----
(parser {} [{[:product/id 1] [:product/brand]}])
; => {[:product/id 1] #:product{:brand "Taylor"}}

(parser {} [{[:product/brand "Taylor"] [:product/brand-id]}])
; => {[:product/brand "Taylor"] #:product{:brand-id 44151}}
----

== Multiple inputs

The input to a resolver is a set, and as such you can require more than one thing as input to your resolvers.  When doing
so, of course, your resolver function will receive all of the inputs requested; however, this also means that the parsing
context needs to contain them, or there must exist other resolvers that can use what's in the context to fill them in.

As you have seen before, the only way to provide ad-hoc information to connect is using the ident query, but in the ident
itself you can only provide one attribute at a time.

Since version `2.2.0-beta11` the ident readers from connect (`ident-reader` and `open-ident-reader`) support adding extra
context to the query using parameters. Let's say you want to load some customer data but you want to reduce the number
of resolvers called by providing some base information that you already have, you can issue a query like this:

[source,clojure]
----
[{([:customer/id 123] {:pathom/context {:customer/first-name "Foo" :customer/last-name "Bar"}})
  [:customer/full-name]}]
----

== Alias resolvers

Sometimes you want to make an alias, that is, create a resolver that just converts one
name to another, for example:

[source,clojure]
----
(pc/defresolver alias-youtube-video [env {:user/keys [youtube-video-url]}]
  {::pc/input  #{:user/youtube-video-url}
   ::pc/output [:youtube.video/id]}
  {:youtube.video/id youtube-video-url})
----

The previous resolver will convert `:user/youtube-video-url` to `:youtube.video/id`. To make
that easy Pathom provides some helpers:

[source,clojure]
----
; this returns a resolver that works just like the previous resolver
(def alias-youtube-video (pc/alias-resolver :user/youtube-video-url :youtube.video/id))
----

If you want to create an alias that goes in both directions, use `pc/alias-resolver2`.

== Parameters

Parameters enable another dimension of information to be added to the request. Params have
different semantics from inputs: inputs are more a `dependency` thing while params are
more like options. In practice, the main difference is that inputs are something Pathom
will try to look up and make available, while parameters must always be provided at
query time, there have no auto resolution.
Common cases to use parameters are: pagination, sorting, filtering...

Let's write a resolver that outputs a sequence of instruments which can optionally be sorted
via a sorting criteria specified via a parameter.

[source,clojure]
----
(pc/defresolver instruments-list [env _]
  {::pc/output [{::instruments [:instrument/id :instrument/brand
                                :instrument/type :instrument/price]}]}
  (let [{:keys [sort]} (-> env :ast :params)] ; <1>
    {::instruments (cond->> instruments
                     (keyword? sort) (sort-by sort))}))
----

<1> Pulls the parameters from the environment

Then we can run queries like:

[source,clojure]
----
[(::instruments {:sort :instrument/brand})]
[(::instruments {:sort :instrument/price})]
[(::instruments {:sort :instrument/type})]

; params with join

[{(::instruments {:sort :instrument/price})
  [:instrument/id
   :instrument/brand]}]
----

Try it out:

++++
<div x-app="interactive-parser" data-parser="connect.parameters" class="loader">
[(::instruments {:sort :instrument/price})]
</div>
<div class="space"></div>
++++

== N+1 Queries and Batch resolvers

When you have a to-many relation that is being resolved by a parser, you will typically end up with a single query that
finds the "IDs", and then `N` more queries to fill in the details of each item in the sequence.  This is known as the
`N+1` problem, and can be a source of significant performance problems.

Instead of running a resolver once for each item on the list, the idea to solve this problem is to send all the inputs
as a sequence, so the resolver can do some optimal implementation to handle multiple items. When this happens,
we call it a batch resolver. For example, let's take a look at the following demo:

[source,clojure]
----
include::example$com/wsscode/pathom/book/connect/batch.cljs[]
----

Try the demo:

++++
<div x-app="interactive-parser" data-parser="connect.batch" class="loader">
[{:items [:number-added]}]
</div>
<div class="space"></div>
++++

NOTE: This demo is using Pathom async parsers.  The resolvers in async parsers can return channels that (eventually)
resolve to the result, which is why you see `go` blocks in the code.  See <<AsyncParsing, Async Parsing>> for more details.
We use them in this demo so we can  "sleep" in a Javascript environment to mimic overhead in processing.
In the rest of the book we recommend using the parallel parser, the reason  to use the async parser in this demo is
that it more easily demonstrates the `N+1` issue.

You can note by the tracer that it took one second for each entry, a clear cascade,
because it had to call the `:number-added` resolver once for each item.

We can improving that by turning this into a batch resolver, like this:

[source,clojure]
----
include::example$com/wsscode/pathom/book/connect/batch2.cljs[]
----

Try the demo:

++++
<div x-app="interactive-parser" data-parser="connect.batch2" class="loader">
[{:items [:number-added]}]
</div>
<div class="space"></div>
++++

Note that this time the sleep of one second only happened once, this is because when Pathom is processing a list and the
resolver supports batching, the resolver will get all the inputs in a single call, so your batch resolver can get all
the items in a single iteration. The results will be cached back for each entry, this will make the other items hit the
cache instead of calling the resolver again.

=== Batch transforms [[resolver-batch-transform]]

Starting on version `2.2.0` Pathom add some helpers to facilitate the creation of batch resolvers using Pathom
<<connect-transform,transform>> facilities.

In the previous example we manually detected if input was a sequence, this API is made this way so the resolver
keeps compatibility with the regular resolver API, but often it is easier if you get a consistent input (always a sequence
for example). We can enforce this using a transform:

[source,clojure]
----
(pc/defresolver slow-resolver [_ input]
  {::pc/input     #{:number}
   ::pc/output    [:number-added]
   ; use the transform, note we removed ::pc/batch? true, that's because the transform
   ; will add this for us
   ::pc/transform pc/transform-batch-resolver}
  (go
    (async/<! (async/timeout 1000))
    ; no need to detect sequence, it is always a sequence now
    (mapv (fn [v] {:number-added (inc (:number v))}) input)))
----

Try the demo:

++++
<div x-app="interactive-parser" data-parser="connect.batch3" class="loader">
[{:items [:number-added]}]
</div>
<div class="space"></div>
++++

Another helper that Pathom provides is to transform a serial resolver that would run
one by one, into a batch that runs at concurrency `n`.

[source,clojure]
----
(pc/defresolver slow-resolver [_ {:keys [number]}]
  {::pc/input     #{:number}
   ::pc/output    [:number-added]
   ; set auto-batch with concurrency of 10
   ::pc/transform (pc/transform-auto-batch 10)}
  (go
    (async/<! (async/timeout 1000))
    ; dealing with the single case, as in the first example we did on batch
    {:number-added (inc number)}))
----

Try the demo:

++++
<div x-app="interactive-parser" data-parser="connect.transform-auto-batch" class="loader">
[{:items [:number-added]}]
</div>
<div class="space"></div>
++++

Note this time we did called resolver fn multiple times but in parallel, the way this may
impact the performance will vary case by case, I suggest giving some thought on the best
strategy for each case individually.

=== Aligning results

Often times when you do a batch request to some service/api the results won't come in
the same order of the request, also the count might not match in case some of the items
on request were invalid. To facilitate the coding of these cases Pathom provides
a helper to correctly sort the results back, for more info check the docs about
link:https://cljdoc.org/d/com.wsscode/pathom/CURRENT/api/com.wsscode.pathom.connect#batch-restore-sort[batch-restore-sort on cljdoc].
